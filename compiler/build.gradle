
apply plugin: 'java'
apply from: file('gradle/idea.gradle')

sourceCompatibility = '1.6'
targetCompatibility = '1.6'

sourceSets {
    main {
        java {
            srcDirs 'src/main/java', 'src/generated-sources/antlr'
        }
    }
}

ext {
    antlrVersion = '4.2'
}

configurations {
    antlr4
    compile.extendsFrom antlr4
}

dependencies {
    antlr4 group: 'org.antlr', name: 'antlr4', version: antlrVersion
    antlr4 group: 'org.antlr', name: 'antlr4-runtime', version: antlrVersion
    runtime 'org.antlr:stringtemplate:4.0.2'
    runtime 'com.google.guava:guava:18.0'

	testCompile 'junit:junit:4.11'
}

task generateParserSource(type: JavaExec) {

    def grammars = fileTree('src/main/antlr').include('*.g4')

    main = 'org.antlr.v4.Tool'
    classpath = configurations.antlr4
    args = ['-o', 'src/generated-sources/antlr/com/devsmart/staple', '-visitor', '-package', 'com.devsmart.staple', grammars.files].flatten()
}

compileJava.dependsOn generateParserSource